&НаКлиенте
Перем Префикс;
&НаКлиенте
Перем ИсходныйТекст;
&НаКлиенте
Перем Суффикс;

&НаКлиенте
Перем ШаблонРВ;

&НаКлиенте
Перем RegExp;
&НаКлиенте
Перем RegExpДоступны;

&НаКлиенте
Перем ИгнорироватьРегистр;
&НаКлиенте
Перем МногострочныйПоиск;
&НаКлиенте
Перем ДоПервогоСовпадения;

//ВСПОМОГАТЕЛЬНЫЕ ОБРАБОТКИ

&НаКлиенте
// Рассчитывает сколько раз Подстрока встречается в ИсхСтроке
//
// Параметры
//  ИсхСтрока  - Строка - строка в которой ищем вхождения подстроки
//  Подстрока  - Строка - какие вхождения ищем
//
// Возвращаемое значение:
//   Число   - число вхождений
//
Функция ЧислоВхождений(ИсхСтрока, Подстрока) Экспорт
	ДлинаИсхСтрока	= СтрДлина(ИсхСтрока);
	ДлинаПодстрока	= СтрДлина(Подстрока);
	ДлинаСокрСтрока	= СтрДлина(СтрЗаменить(ИсхСтрока, Подстрока, ""));
	
	Возврат (ДлинаИсхСтрока - ДлинаСокрСтрока) / ДлинаПодстрока;
КонецФункции // ЧислоВхождений()

&НаКлиенте
Функция НадоЭкранировать(ТекСимвол)
	Возврат ТекСимвол = "[" Или
			ТекСимвол = "]" Или
			ТекСимвол = "\" Или
			ТекСимвол = "/" Или
			ТекСимвол = "^" Или
			ТекСимвол = "$" Или
			ТекСимвол = "." Или
			ТекСимвол = "|" Или
			ТекСимвол = "?" Или
			ТекСимвол = "*" Или
			ТекСимвол = "+" Или
			ТекСимвол = "(" Или
			ТекСимвол = ")" Или
			ТекСимвол = "{" Или
			ТекСимвол = "}";
КонецФункции

&НаКлиенте
Функция Экранировать(ТекСимвол)
	Если НадоЭкранировать(ТекСимвол) Тогда
		Возврат "\" + ТекСимвол;
	Иначе
		Возврат ТекСимвол;
	КонецЕсли;
КонецФункции

&НаКлиенте
Функция АвтоматическоеЭкранирование(ВхСтрока)
	Рез = "";
	Для Сч = 1 По СтрДлина(ВхСтрока) Цикл
		ТекСимвол = Сред(ВхСтрока, Сч, 1);
		Рез = Рез + Экранировать(ТекСимвол);
	КонецЦикла;
	
	Возврат Рез
КонецФункции

//РАБОТА С СУФФИКСАМИ И ПРЕФИКСАМИ

&НаКлиенте
// Устанавливает/снимает признак работы выражения в начале строки
//
// Параметры:
//  Флаг  - Булево - признак установить или снять признак поиска
//
// Возвращаемое значение:
//   Форма   - эта же форма
//
Функция ВНачалеСтроки(Флаг = Истина) Экспорт

	Если Флаг Тогда
		Префикс = Префикс + "^";
	Иначе
		Префикс = СтрЗаменить(Префикс, "^", "");
	КонецЕсли;
	
	Возврат ЭтаФорма;

КонецФункции // НачалоСтроки()

&НаКлиенте
// Устанавливает/снимает признак работы выражения в конце строки
//
// Параметры:
//  Флаг  - Булево - признак установить или снять признак поиска
//
// Возвращаемое значение:
//   Форма   - эта же форма
//
Функция ВКонцеСтроки(Флаг = Истина) Экспорт

	Если Флаг Тогда
		Суффикс = Суффикс + "$";
	Иначе
		Суффикс = СтрЗаменить(Суффикс, "$", "");
	КонецЕсли;
	
	Возврат ЭтаФорма;

КонецФункции // ИскатьСКонца()


//ОСНОВНЫЕ ПРОЦЕДУРЫ ДОБАВЛЕНИЯ ДАННЫХ

&НаКлиенте
// Добавляет очередной кусок регулярного выражения
//
// Параметры:
//  Параметр  - Строка, Форма - строка поиска или готовое вербальное выражение
//
// Возвращаемое значение:
//   Форма   - эта форма
//
Функция Добавить(Параметр) Экспорт 

	Если ТипЗнч(Параметр) = Тип("Строка") Тогда
		Возврат ДобавитьСтроку(Параметр);
	ИначеЕсли ТипЗнч(Параметр) = Тип("Форма") Тогда
		Возврат ДобавитьВербВыражение(Параметр);
	КонецЕсли;
	
	Возврат ЭтаФорма;

КонецФункции // Добавить()

&НаКлиенте
// Добавляет очередной кусок регулярного выражения
//
// Параметры:
//  ВхСтрока  - Строка - строка поиска
//
// Возвращаемое значение:
//   Форма   - эта форма
//
Функция ДобавитьСтроку(ВхСтрока)
	ДобавляемаяСтрока = "";
	Если Прав(Суффикс, 1) = "?" Тогда
		ДобавляемаяСтрока = "?";
		Суффикс = Лев(Суффикс, СтрДлина(Суффикс) - 1);
	КонецЕсли;
	
	ИсходныйТекст = ИсходныйТекст + ВхСтрока + ДобавляемаяСтрока;
	
	Возврат ЭтаФорма;

КонецФункции // ДобавитьСтроку()

&НаКлиенте
// Добавить уже подготовленное вербальное выражение
//
// Параметры:
//  ВербВыражение  - Форма - подготовленое вербальное выражение
//
// Возвращаемое значение:
//   Форма   - эта форма
//
Функция ДобавитьВербВыражение(ВербВыражение)

	Возврат ЭтаФорма.НачатьГруппу().Добавить(ВербВыражение.ПолучитьСтроку()).ЗакончитьГруппу();

КонецФункции // ДобавитьВербВыражение()

&НаКлиенте
// Аналог Добавить для удобной записи
//
// Параметры:
//  ВхСтрока  - Строка - Строка поиска
//
// Возвращаемое значение:
//   Форма   - эта форма
//
Функция Затем(Параметр) Экспорт
	Если ТипЗнч(Параметр) = Тип("Строка") Тогда
		Возврат ЭтаФорма.Добавить("(?:" + АвтоматическоеЭкранирование(Параметр) + ")");
	Иначе
		Возврат ЭтаФорма.Добавить("(?:" + Параметр.ПолучитьСтроку() + ")"); //группировка без обратной связи
															// группа используется только для связи		
	КонецЕсли;

КонецФункции // Затем()

&НаКлиенте
// Аналог Добавить для удобной записи
//
// Параметры:
//  ВхСтрока  - Строка - Строка поиска
//
// Возвращаемое значение:
//   Форма   - эта форма
//
Функция Потом(Параметр) Экспорт

	Возврат Затем(Параметр);

КонецФункции // Потом()

&НаКлиенте
// Аналог Добавить для удобной записи
// Использовать в начале выражения
//
// Параметры:
//  Параметр  - Строка, Форма - строка поиска или готовое вербальное выражение
//
// Возвращаемое значение:
//   Форма   - эта форма
//
Функция Найди(Параметр) Экспорт
	Если ТипЗнч(Параметр) = Тип("Строка") Тогда
		Возврат ЭтаФорма.Добавить(АвтоматическоеЭкранирование(Параметр));
	ИначеЕсли ТипЗнч(Параметр) = Тип("Форма") Тогда
		Возврат ЭтаФорма.Добавить(Параметр);
	КонецЕсли;

КонецФункции // Найди()

&НаКлиенте
// Проверка строки/выражения которого может не быть
//
// Параметры:
//  Параметр  - Строка, Форма - строка поиска или готовое вербальное выражение
//                 
// Возвращаемое значение:
//   ЭтаФорма   - эта форма
//
Функция МожетБыть(Параметр) Экспорт

	Возврат ЭтаФорма.Возможно(Параметр);

КонецФункции // МожетБыть()

&НаКлиенте
// Проверка строки/выражения которого может не быть
//
// Параметры:
//  Параметр  - Строка, Форма - строка поиска или готовое вербальное выражение
//                 
// Возвращаемое значение:
//   ЭтаФорма   - эта форма
//
Функция Возможно(Параметр) Экспорт

	Если ТипЗнч(Параметр) = Тип("Строка") Тогда
		Возврат ВозможноСтрока(Параметр);
	ИначеЕсли ТипЗнч(Параметр) = Тип("Форма") Тогда
		Возврат ВозможноВербВыражение(Параметр);
	КонецЕсли;
	
	Возврат ЭтаФорма;

КонецФункции // МожетБыть()

&НаКлиенте
// Проверка строки, которой может не быть
//
// Параметры:
//  ВхСтрока  - Строка - строка поиска
//
// Возвращаемое значение:
//   Форма   - эта форма
//
Функция ВозможноСтрока(ВхСтрока)

	Возврат ЭтаФорма.Затем(ВхСтрока).Добавить("?");

КонецФункции // ВозможноСтрока()

&НаКлиенте
// Проверка вербального выражения, которого может не быть
//
// Параметры:
//  ВербВыражение  - Форма - подготовленое вербальное выражение
//
// Возвращаемое значение:
//   Форма   - эта форма
//
Функция ВозможноВербВыражение(ВербВыражение)

	Возврат ЭтаФорма.НачатьГруппу().Добавить(ВербВыражение).ЗакончитьГруппу().Добавить("?");

КонецФункции // ВозможноВербВыражение()

&НаКлиенте
// Объявляет начало установки признака, что дальнейшее выражение возможно отсутствует
//
// Возвращаемое значение:
//   Форма   - эта форма
//
Функция ДальшеМожетБыть() Экспорт

	Суффикс = Суффикс + "?";
	
	Возврат ЭтаФорма;

КонецФункции // ДальшеМожетБыть()


//МОДИФИАКТОРЫ ПОИСКА
//БольшеОдного, Все кроме и прочие

&НаКлиенте
// Выражение совпадает с любой, даже пустой строкой
//
// Возвращаемое значение:
//   Форма   - эта форма
//
Функция ЧтоУгодно() Экспорт

	Возврат ЭтаФорма.Добавить("(?:.*)");

КонецФункции // ЧтоУгодно()

&НаКлиенте
// Выражение совпадает с любой строкой кроме указанной
//
// Параметры:
//  СтрокаИсключение  - Строка - строка которую надо исключить
//
// Возвращаемое значение:
//   Форма   - эта форма
//
Функция ЧтоУгодноКроме(СтрокаИсключение) Экспорт

	Возврат ЭтаФорма.Добавить("(?:[^" + СтрокаИсключение + "]*)");							

КонецФункции // ВсеКроме()

&НаКлиенте
// Выражение совпадает с любой строкой, кроме пустой
//
// Возвращаемое значение:
//   Форма   - эта форма
//
Функция ЧтоНибудь() Экспорт

	Возврат ЭтаФорма.Добавить("(?.+)");

КонецФункции // ЧтоНибудь()

&НаКлиенте
// Выражение совпадает с любой непустой строкой, кроме указаной
//
// Параметры:
//  СтрокаИсключение  - Строка - строка которую надо исключить
//
// Возвращаемое значение:
//   Форма   - эта форма
//
Функция ЧтоНибудьКроме(СтрокаИсключения) Экспорт 

	Возврат ЭтаФорма.Добавить("(?:[^" + СтрокаИсключения + "]+)");

КонецФункции // ЧтоНибудьКроме()

&НаКлиенте
// Дальше любой из перечисленных символов
//
// Параметры:
//  Символы  - строка - строка с перечислением символов
//
// Возвращаемое значение:
//   Форма   - эта форма
//
Функция ЛюбойИз(Символы) Экспорт

	Возврат ЭтаФорма.Добавить("[" + Символы + "]");

КонецФункции // ЛюбойИз()

&НаКлиенте
Функция Любой(Символы) Экспорт

	Возврат ЛюбойИз(Символы);

КонецФункции // Любой()

&НаКлиенте
// Символы из указанного диапазона (10 вариантов диапазонов)
//
// Параметры:
//  С  - Строка - первый символ диапазона
//  По  - Строка - последний символ диапазона
//
// Возвращаемое значение:
//   Форма   - эта форма
//
Функция Диапазон(С0, По0,
					С1 = "", По1 = "",
					С2 = "", По2 = "",
					С3 = "", По3 = "",
					С4 = "", По4 = "",
					С5 = "", По5 = "",
					С6 = "", По6 = "",
					С7 = "", По7 = "",
					С8 = "", По8 = "",
					С9 = "", По9 = "") Экспорт
	Врем = "[" + С0 + "-" + По0;
	Если Не (ПустаяСтрока(С1) или ПустаяСтрока(По1)) Тогда
		Врем = Врем + С1 + "-" + По1;
	КонецЕсли;
	Если Не (ПустаяСтрока(С2) или ПустаяСтрока(По2)) Тогда
		Врем = Врем + С2 + "-" + По2;
	КонецЕсли;
	Если Не (ПустаяСтрока(С3) или ПустаяСтрока(По3)) Тогда
		Врем = Врем + С3 + "-" + По3;
	КонецЕсли;
	Если Не (ПустаяСтрока(С4) или ПустаяСтрока(По4)) Тогда
		Врем = Врем + С4 + "-" + По4;
	КонецЕсли;
	Если Не (ПустаяСтрока(С5) или ПустаяСтрока(По5)) Тогда
		Врем = Врем + С5 + "-" + По5;
	КонецЕсли;
	Если Не (ПустаяСтрока(С6) или ПустаяСтрока(По6)) Тогда
		Врем = Врем + С6 + "-" + По6;
	КонецЕсли;
	Если Не (ПустаяСтрока(С7) или ПустаяСтрока(По7)) Тогда
		Врем = Врем + С7 + "-" + По7;
	КонецЕсли;
	Если Не (ПустаяСтрока(С8) или ПустаяСтрока(По8)) Тогда
		Врем = Врем + С8 + "-" + По8;
	КонецЕсли;
	Если Не (ПустаяСтрока(С9) или ПустаяСтрока(По9)) Тогда
		Врем = Врем + С9 + "-" + По9;
	КонецЕсли;
	Врем = Врем + "]";
	
	Возврат ЭтаФорма.Добавить(Врем);
	
КонецФункции // Диапазон()

&НаКлиенте
// Строка встречается один раз или больше
//
// Возвращаемое значение:
//   Форма   - эта форма
//
Функция ОдинРазИлиБольше() Экспорт

	Возврат ЭтаФорма.Добавить("+");

КонецФункции // ОдинРазИлиБольше()

&НаКлиенте
// Строка встречается любое число раз, даже отсутствует
//
// Возвращаемое значение:
//   Форма   - Эта форма
//
Функция ЛюбоеЧислоРаз() Экспорт

	Возврат ЭтаФорма.Добавить("*");

КонецФункции // ЛюбоеЧислоРаз()

&НаКлиенте
// Строка встречается N раз
//
// Параметры:
//  Количество  - Число - скольк раз точно должна встретится строка
//
// Возвращаемое значение:
//   Форма   - эта форма
//
Функция Раз(Количество) Экспорт
	
	ИсходныйТекст = ИсходныйТекст + "{" +
					Формат(Количество, "ЧДЦ=; ЧН=; ЧГ=") + 
					"}";
	Возврат ЭтаФорма;
КонецФункции // Раз()

&НаКлиенте
// Строка встречается от N до M раз
//
// Параметры:
//  От  - Число - минимальное количество вхождений
//  По  - Число - маскимальное количество вхождений
//
// Возвращаемое значение:
//   Форма   - эта форма
//
Функция ОтДо(От, До) Экспорт

	ИсходныйТекст = ИсходныйТекст + "{" +
					Формат(От, "ЧДЦ=; ЧН=; ЧГ=") + 
					"," +
					Формат(До, "ЧДЦ=; ЧН=; ЧГ=") + 
					"}";
	Возврат ЭтаФорма;	

КонецФункции // ОтДо()

&НаКлиенте
// Строка встречается хотя N раз
//
// Параметры:
//  Количество  - Число - минимальное количество вхождений
//
// Возвращаемое значение:
//   Форма   - эта форма
//
Функция ХотяБы(Количество) Экспорт

	ИсходныйТекст = ИсходныйТекст + "{" +
					Формат(Количество, "ЧДЦ=; ЧН=; ЧГ=") + 
					"," +
					"" + 
					"}";
	Возврат ЭтаФорма;

КонецФункции // ХотяБы()

&НаКлиенте
// Строка встречается не больше чем N раз
//
// Параметры:
//  Количество  - Число - максимальное количество вхождений
//                 
// Возвращаемое значение:
//   Форма   - эта форма
//
Функция НеБольшеЧем(Количество)

	ИсходныйТекст = ИсходныйТекст + "{" +
					""
					"," +
					Формат(Количество, "ЧДЦ=; ЧН=; ЧГ=") + 
					"}";
	Возврат ЭтаФорма;

КонецФункции // НеБольшеЧем()


//УСЛОВИЯ ПОИСКА

&НаКлиенте
// Условие выбора строки
//
// Параметры:
//  Строка  - строка - строка поиска
//
// Возвращаемое значение:
//   Форма   - эта форма
//
Функция _Или(Строка) Экспорт

	Префикс = Префикс + "(?:";
	ОткрытыхСкобок = ЧислоВхождений(Префикс, "(");
	ЗакрытыхСкобок = ЧислоВхождений(Префикс, ")");
	
	Если ОткрытыхСкобок >= ЗакрытыхСкобок Тогда
		Суффикс = ")" + Суффикс;
	КонецЕсли;
	
	ЭтаФорма.Добавить(")|(?:" + Строка);
	
	Возврат ЭтаФорма;

КонецФункции // _Или()

&НаКлиенте
// Совпадает с одной указанных строк
//
// Параметры:
//  Параметр0..9  - Строка - строки поиска
//
// Возвращаемое значение:
//   Форма   - эта форма
//
Функция ОдинИз(Параметр0,
				Параметр1 = Неопределено, Параметр2 = Неопределено, Параметр3 = Неопределено,
				Параметр4 = Неопределено, Параметр5 = Неопределено, Параметр6 = Неопределено,
				Параметр7 = Неопределено, Параметр8 = Неопределено, Параметр9 = Неопределено) Экспорт
	Врем = "(?:";
	Врем = Врем + "(?:" +
					Параметр0 + 
					")";
	
	Если Параметр1 <> Неопределено Тогда
		Врем = Врем + "|(?:" +
				Параметр1 + 
				")";
	КонецЕсли;
	Если Параметр2 <> Неопределено Тогда
		Врем = Врем + "|(?:" +
				Параметр2 + 
				")";
	КонецЕсли;
	Если Параметр3 <> Неопределено Тогда
		Врем = Врем + "|(?:" +
				Параметр3 + 
				")";
	КонецЕсли;		
	Если Параметр4 <> Неопределено Тогда
		Врем = Врем + "|(?:" +
				Параметр4 + 
				")";
	КонецЕсли;
	Если Параметр5 <> Неопределено Тогда
		Врем = Врем + "|(?:" +
				Параметр5 + 
				")";
	КонецЕсли;
	Если Параметр6 <> Неопределено Тогда
		Врем = Врем + "|(?:" +
				Параметр6 + 
				")";
	КонецЕсли;
	Если Параметр7 <> Неопределено Тогда
		Врем = Врем + "|(?:" +
				Параметр7 + 
				")";
	КонецЕсли;
	Если Параметр8 <> Неопределено Тогда
		Врем = Врем + "|(?:" +
				Параметр8 + 
				")";
	КонецЕсли;
	Если Параметр9 <> Неопределено Тогда
		Врем = Врем + "|(?:" +
				Параметр9 + 
				")";
	КонецЕсли;
	Врем = Врем + ")";
	
	Возврат ЭтаФорма.Добавить(Врем);	

КонецФункции // ОдинИз()


//РАБОТА С ГРУППИРОВКАМИ

&НаКлиенте
// Начало формирования группировки
//
// Возвращаемое значение:
//   Форма   - эта форма
//
Функция Захватить() Экспорт

	Суффикс = Суффикс + ")";
	Возврат ЭтаФорма.Добавить("(");

КонецФункции // Захватить()

&НаКлиенте
// Начало формирования группировки без сохранения результата
//
// Возвращаемое значение:
//   Форма   - эта форма
//
Функция НачатьГруппу() Экспорт

	Суффикс = Суффикс + ")";
	Возврат ЭтаФорма.Добавить("(?:");

КонецФункции // НачатьГруппу()

&НаКлиенте
// Заканчивает группировку
//
// Возвращаемое значение:
//   Форма   - эта форма
//
Функция ЗакончитьЗахват() Экспорт

	Если Прав(Суффикс, 1) = ")" Тогда
		ДобавляемаяСтрока = ")";
		Суффикс = Лев(Суффикс, СтрДлина(Суффикс) - 1);
		Если Прав(Суффикс, 1) = "?" Тогда
			ДобавляемаяСтрока = ДобавляемаяСтрока + "?";
			Суффикс = Лев(Суффикс, СтрДлина(Суффикс) - 1);
		КонецЕсли;
		Возврат ЭтаФорма.Добавить(ДобавляемаяСтрока);
	Иначе
		ВызватьИсключение "Не могу закончить группировку, так как группировка не начиналась";
	КонецЕсли;
	
КонецФункции // ЗакончитьЗахват()

&НаКлиенте
// Заканчивает группировку
//
// Возвращаемое значение:
//   Форма   - эта форма
//
Функция ЗакончитьГруппу() Экспорт

	ЭтаФорма.ЗакончитьЗахват();

	Возврат ЭтаФорма;
КонецФункции // ЗакончитьГруппу()


//КРАСИВЫЕ ОБЕРТКИ

&НаКлиенте
Функция КонецСтроки() Экспорт
	Возврат ЭтаФорма.Добавить("(?:\n|(?:\r\n)|(?:\r\r))");
КонецФункции

&НаКлиенте
Функция КС() Экспорт
	Возврат ЭтаФорма.КонецСтроки();
КонецФункции

&НаКлиенте
Функция Табуляция() Экспорт
	Возврат ЭтаФорма.Добавить("(?:\t)");
КонецФункции

&НаКлиенте
Функция Таб() Экспорт
	Возврат ЭтаФорма.Табуляция();
КонецФункции

&НаКлиенте
// Признак того, что дальше идет слово на английском
//
// Возвращаемое значение:
//   Форма   - эта форма
//
Функция СловоАнгл() Экспорт 

	Возврат ЭтаФорма.Добавить("(?:\w+)");

КонецФункции // СловоАнгл()

&НаКлиенте
// Признак того, что дальше идет слово на русском
//
// Возвращаемое значение:
//   Форма   - эта форма
//
Функция СловоРус() Экспорт

	Возврат ЭтаФорма.Добавить("[а-яА-ЯёЁ_0-9]+");

КонецФункции // СловоРус()

&НаКлиенте
// Признак того, что дальше идет слово на любом языке
//
// Возвращаемое значение:
//   Форма   - эта форма
//
Функция Слово() Экспорт

	Возврат ЭтаФорма.Добавить("[\wа-яА-ЯёЁ0-9]+");

КонецФункции // Слово()

&НаКлиенте
// Буква английского алфавита
//
// Возвращаемое значение:
//   Форма   - Эта форма
//
Функция БукваАнгл() Экспорт

	Возврат ЭтаФорма.Добавить("(?:\w)");

КонецФункции // БукваАнгл()

&НаКлиенте
// Буква русского алфавита
//
// Возвращаемое значение:
//   Форма   - Эта форма
//
Функция БукваРус() Экспорт

	Возврат ЭтаФорма.Добавить("(?:[а-яА-ЯёЁ_0-9])");

КонецФункции // БукваАнгл()

&НаКлиенте
// Буква любого алфавита
//
// Возвращаемое значение:
//   Форма   - Эта форма
//
Функция Буква() Экспорт

	Возврат ЭтаФорма.Добавить("(?:[\wа-яА-ЯёЁ_0-9])");

КонецФункции // БукваАнгл()

&НаКлиенте
// Нн буква английского алфавита
//
// Возвращаемое значение:
//   Форма   - Эта форма
//
Функция НеБукваАнгл() Экспорт

	Возврат ЭтаФорма.Добавить("(?:\W)");

КонецФункции // БукваАнгл()

&НаКлиенте
// Не буква русского алфавита
//
// Возвращаемое значение:
//   Форма   - Эта форма
//
Функция НеБукваРус() Экспорт

	Возврат ЭтаФорма.Добавить("(?:[^а-яА-ЯёЁ_0-9])");

КонецФункции // БукваАнгл()

&НаКлиенте
// Не буква любого алфавита
//
// Возвращаемое значение:
//   Форма   - Эта форма
//
Функция НеБуква() Экспорт

	Возврат ЭтаФорма.Добавить("(?:[^\wа-яА-ЯёЁ_0-9])");

КонецФункции // БукваАнгл()

&НаКлиенте
// Это любая цифра от 0 - 9
//
// Возвращаемое значение:
//   Форма   - эта форма
//
Функция Цифра() Экспорт

	Возврат ЭтаФорма.Добавить("(?:\d)");

КонецФункции // Цифра()

&НаКлиенте
Функция НеЦифра() Экспорт
	
	Возврат ЭтаФорма.Добавить("(?:\D)");
	
КонецФункции

&НаКлиенте
// Любой пробельный символ: пробел, неразрывный пробел
//
// Возвращаемое значение:
//   Форма   - эта форма
//
Функция Пробел() Экспорт

	Возврат ЭтаФорма.Добавить("(?:\s");

КонецФункции // Пробел()

&НаКлиенте
// Любой непробельный символ: пробел, неразрывный пробел
//
// Возвращаемое значение:
//   Форма   - эта форма
//
Функция НеПробел()

	Возврат ЭтаФорма.Добавить("(?:\S");

КонецФункции // НеПробел()


//МОДИФИКАТОРЫ ПОИСКА

&НаКлиенте
Функция ИгнорироватьРегистр() Экспорт
	ИгнорироватьРегистр = Истина;
	Возврат ЭтаФорма;
КонецФункции

&НаКлиенте
Функция СУчетомРегистра() Экспорт
	ИгнорироватьРегистр = Ложь;
	Возврат ЭтаФорма
КонецФункции

&НаКлиенте
Функция ДоПервогоСовпадения() Экспорт
	ДоПервогоСовпадения = Истина;
	Возврат ЭтаФорма;
КонецФункции

&НаКлиенте
Функция ГлобальныйПоиск() Экспорт
	ДоПервогоСовпадения = Ложь;
	Возврат ЭтаФорма;
КонецФункции

&НаКлиенте
Функция ТолькоВОднойСтроке() Экспорт
	МногострочныйПоиск = Ложь;
	Возврат ЭтаФорма
КонецФункции

&НаКлиенте
Функция ВоВсехСтроках() Экспорт
	МногострочныйПоиск = Истина;
	Возврат ЭтаФорма;
КонецФункции


//КВАНТИФИКАТОРЫ

&НаКлиенте
Функция МожноПрименятьКвантификатор()
	ПоследнийСимвол = Прав(ИсходныйТекст, 1);
	
	Возврат ПоследнийСимвол = "*" Или 
			ПоследнийСимвол = "+" Или
			ПоследнийСимвол = "}" Или
			ПоследнийСимвол = "?";
	
КонецФункции

&НаКлиенте
Функция Ленивый() Экспорт
	Если Не МожноПрименятьКвантификатор() Тогда
		ВызватьИсключение "Нельзя применить квантификатор поиска";
	КонецЕсли;
	Возврат ЭтаФорма.Добавить("?");
КонецФункции

&НаКлиенте
Функция Сверхжадный() Экспорт
	Если Не МожноПрименятьКвантификатор() Тогда
		ВызватьИсключение "Нельзя применить квантификатор поиска";
	КонецЕсли;
	
	Возврат ЭтаФорма.Добавить("+");
КонецФункции

//RegExp --------------------------------------------------------

&НаКлиенте
Функция regexp() Экспорт
	Префикс			= "";
	ИсходныйТекст	= "";
	Суффикс			= "";
	
	ИгнорироватьРегистр	= Истина;
	МногострочныйПоиск	= Ложь;
	ДоПервогоСовпадения = Ложь;
	
	Если RegExp = Неопределено Тогда //Нужна инициализация
		Попытка
        	RegExp = Новый COMОбъект("VBScript.RegExp");    // создаем объект для работы с регулярными выражениями
		Исключение
			RegExp = Неопределено;
			ВызватьИсключение "Не удалось использовать Регулярные выражения";			
		КонецПопытки;
	КонецЕсли;
	Префикс			= "";
	ИсходныйТекст	= "";
	Суффикс			= "";
	ШаблонРВ		= "";
	
	Возврат ЭтаФорма;
КонецФункции

&НаКлиенте
Функция РегулярныеВыражения_Выполнить(АнализируемыйТекст)

    РезультатАнализаСтроки = RegExp.Execute(АнализируемыйТекст);

    МассивВыражений = Новый Массив;

    Для Каждого Выражение Из РезультатАнализаСтроки Цикл
        СтруктураВыражение = Новый Структура ("Начало, Длина, Значение, ПодВыражения", 
												Выражение.FirstIndex, 
												Выражение.Length, 
												Выражение.Value);

        //Обработка подвыражений
        МассивПодВыражений = Новый Массив;
        Для Каждого ПодВыражение Из Выражение.SubMatches Цикл
            МассивПодВыражений.Добавить(ПодВыражение);
        КонецЦикла;
        СтруктураВыражение.ПодВыражения = МассивПодВыражений;

        МассивВыражений.Добавить (СтруктураВыражение);

    КонецЦикла;

    Возврат МассивВыражений;

КонецФункции

&НаКлиенте
Функция РегулярныеВыражения_Проверка(ПроверяемыйТекст)

    Возврат RegExp.Test(ПроверяемыйТекст);

КонецФункции

&НаКлиенте
Процедура РегулярныеВыражения_Инициализация ()

	Если RegExp = Неопределено Тогда //Нужна инициализация
		Возврат;
	КонецЕсли;

    //Заполняем данные
    RegExp.MultiLine	= МногострочныйПоиск;                  // истина — текст многострочный, ложь — одна строка
    RegExp.Global		= Не ДоПервогоСовпадения;   // истина — поиск по всей строке, ложь — до первого совпадения
    RegExp.IgnoreCase	= ИгнорироватьРегистр;        // истина — игнорировать регистр строки при поиске
    RegExp.Pattern		= ШаблонРВ;                        // шаблон (регулярное выражение)

КонецПроцедуры

//RegExp --------------------------------------------------------

&НаКлиенте
// Ключевая функция. Возвращает подготовленное регулярное выражение как строку
//
// Возвращаемое значение:
//   Строка   - регулярное выражение на основе вербального
//
Функция ПолучитьСтроку() Экспорт
	
	ШаблонРВ = Префикс + ИсходныйТекст + Суффикс;
	Возврат ШаблонРВ;

КонецФункции // ПолучитьСтроку()

&НаКлиенте
// Ключевая функция. Возвращает подготовленное регулярное выражение как объект
// для дальнейшего использования
//
// Возвращаемое значение:
//   Форма   - эта форма
//
Функция Создать() Экспорт

	ШаблонРВ = Префикс + ИсходныйТекст + Суффикс;
	РегулярныеВыражения_Инициализация();
	Возврат ЭтаФорма;

КонецФункции // Создать()


//Функции работы с регулярными выражениями

&НаКлиенте
Функция СоответствуетШаблону(ПроверяемыйТекст) Экспорт 
	Возврат РегулярныеВыражения_Проверка(ПроверяемыйТекст);
КонецФункции

&НаКлиенте
Функция РазобратьСтрокуПоШаблону(ПроверяемыйТекст) Экспорт
	Возврат РегулярныеВыражения_Выполнить(ПроверяемыйТекст);
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
КонецПроцедуры


